# GITHUB_TOKEN should be set for Github provider to work
# GITHUB_ORGANIZATION should be set if deploying in another ORG and not your
# https://github.com/particuleio/terraform-kubernetes-addons/blob/main/flux2.tf
module "addons_flux" {
  source  = "particuleio/addons/kubernetes"
  version = "2.14.0"
  flux2 = {
    enabled                  = var.flux_enabled
    create_ns                = true
    namespace                = "flux-system"
    target_path              = var.flux_target_path
    default_network_policy   = true
    version                  = var.flux_version
    github_url               = var.flux_github_url
    create_github_repository = false
    repository               = var.flux_repo
    repository_visibility    = ""
    github_token             = ""
    branch                   = var.flux_branch
    flux_sync_branch         = ""
    default_components       = var.flux_default_components
    components               = []
    provider                 = "github"
    auto_image_update        = var.flux_auto_image_update
    custom_kustomize         = local.custom_kustomize
    known_hosts              = [local.known_hosts]
  }
}


locals {
  known_hosts = "github.com ssh-rsa AAAAB3NzaC1yc2EAAAABIwAAAQEAq2A7hRGmdnm9tUDbO9IDSwBK6TbQa+PXYPCPy6rbTrTtw7PHkccKrpp0yVhp5HdEIcKr6pLlVDBfOLX9QUsyCOV0wzfjIJNlGEYsdlLJizHhbn2mUjvSAHQqZETYP81eFzLQNnPHt4EVVUh7VfDESU84KezmD5QlWpXLmvU31/yMf+Se8xhHTvKSCZIFImWwoG6mbUoWf9nzpIoaSjB+weqqUUmpaaasXVal72J+UX2B+2RPW3RcT0eOzQgqlJL3RKrTJvdsjE3JEAvGq3lGHSZXy28G3skua2SmVi/w4yCE6gbODqnTWlg7+wC604ydGXA8VJiS5ap43JXiUFFAaQ=="
  repos_str   = [for src in var.extra_flux_sources : src.source_name]
  ecr_sync_job_ks     = var.ecr_sync_job ? "- ecr-sync.yaml" : ""
  custom_kustomize    = <<YAML
# 
# READ ONLY
# This file is generated by terraform, any direct edits will be overwritten on the next apply
apiVersion: kustomize.config.k8s.io/v1beta1
kind: Kustomization
resources:
- gotk-sync.yaml
- gotk-components.yaml
${local.ecr_sync_job_ks}
${join("\n", formatlist("- %s.yaml", local.repos_str))}
YAML
}
